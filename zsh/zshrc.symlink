# shortcut to this dotfiles path is $DOTFILES
export DOTFILES=$HOME/.dotfiles

# do system files first since we need paths .zsh file in this rep
for config_file ($DOTFILES/system/*.zsh) [[ "$(basename $config_file)" == "completion.zsh" ]] || source $config_file

# source every .zsh file in this rep except system and completions
for config_file ($DOTFILES/**/*.zsh); do
  if [[ "$(basename $config_file)" != "completion.zsh" && "$(dirname $config_file)" != "$DOTFILES/system" ]]; then
    source $config_file
  fi
done

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
if [[ -a ~/.localrc ]]
then
  source ~/.localrc
fi

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit

# load every completion after autocomplete loads
for config_file ($DOTFILES/**/completion.zsh) source $config_file

# set tab title to cwd
precmd () {
  tab_label=${PWD/${HOME}/\~} # use 'relative' path
  echo -ne "\e]2;${tab_label}\a" # set window title to full string
  echo -ne "\e]1;${tab_label: -24}\a" # set tab title to rightmost 24 characters
}
